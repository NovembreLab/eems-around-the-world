
N_EEMS_RUNS=4

gidscript="scripts/geoinddist.R"
rule geo_ind_dist:
    input:
        geodist="dists/{name}.geodist0",
        indiv_meta="subset/{name}.indiv_meta",
        script=gidscript,
    output:
        "dists/{name}.geoinddist"
    script: "../" + gidscript
rule eems_ind_dist:
    input:
        geodist="dists/{name}.eemsdist0",
        indiv_meta="subset/{name}.indiv_meta",
        script=gidscript,
    output:
        "dists/{name}.eemsinddist"
    script: "../" + gidscript

pcindscript="scripts/pcinddist.R"
rule pc_ind_dist:
    input:
        pc='pca/flash_{name}_dim100.pc',
        fam='subset/{name}.fam',
        indiv_meta='subset/{name}.indiv_meta',
        pop_display=_POP_DISPLAY_,
        script = pcindscript
    output:
        pcdist="dists/{name}_dim{npc, \d+}.pcinddist"
    script:  "../" + pcindscript

pcpopscript="scripts/pcdist.R"
rule pc_pop_dist:
    input:
        "pca/median_{name}_dim100.pc",
        script = pcpopscript
    output:
        "dists/{name}_dim{npc, \d+}.pcdist"
    script:  "../" + pcpopscript

pcgridscript="scripts/pcdistgrid.R"
rule pc_grid_dist:
    input:
        pcs="pca/median_{name}_dim100.pc",
        popgrid="dists/{name}.popgrid",
        script = pcgridscript
    output:
        "dists/{name}_dim{npc, \d+}.pcgriddist"
    script:  "../" + pcgridscript

geopopscript="scripts/geodist.R"
rule geo_dist:
    input:
        "subset/{name}.pop_geo",
        script = geopopscript
    output:
        geodist="dists/{name}.geodist",
        geodist0="dists/{name}.geodist0"
    script:  "../" + geopopscript

gendistscript = "scripts/gendist.R"
rule gen_dist:
    input:
        indiv_meta="subset/{name}.indiv_meta",
        diffs="eems/{name}.diffs",
        order="eems/{name}.order",
        script=gendistscript
    output:
        "dists/{name}.gendist",
        "dists/{name}.geninddist",
    script: "../" + gendistscript

eemsdistscript = "scripts/eemsdist.R"
rule eems_pop_dist:
    input:
        Dhat=expand("eemsout/{i}/{name}/rdistJtDhatJ.txt",
            i = range(N_EEMS_RUNS), 
            name = ['{name}']),
        ipmap='eemsout/0/{name}/ipmap.txt',
        order="eems/{name}.order",
        indiv_meta="subset/{name}.indiv_meta",
        script=eemsdistscript
    output:
        "dists/{name}.eemsdist",
        "dists/{name}.popgrid",
        "dists/{name}.eemsdist0",
    params:
        statname='eemsdist'
    script: "../" + eemsdistscript

rule eems0_pop_dist:
    input:
        Dhat=expand("eemsout0/{i}/{name}/rdistJtDhatJ.txt",
            i = range(N_EEMS_RUNS), 
            name = ['{name}']),
        ipmap='eemsout0/0/{name}/ipmap.txt',
        order="eems/{name}.order",
        indiv_meta="subset/{name}.indiv_meta",
        script=eemsdistscript
    params:
        statname='eems0dist'
    output:
        "dists/{name}.eems0dist",
        "dists/{name}.popgrid0",
    script: "../" + eemsdistscript

eemsgridscript="scripts/eems_grid_dist.R"
rule eems_grid_dist:
    input:
        mat=expand("eemsout/{i}/{name}/rdistJtDhatJ.txt",
            i = range(N_EEMS_RUNS), 
            name = ['{name}']),
        script=eemsgridscript
    params:
        statname='eemsdist'
    output:
        "dists/{name}.eemsgriddist",
    script: "../" + eemsgridscript

rule gen_grid_dist:
    input:
        mat=expand("eemsout/{i}/{name}/rdistJtDobsJ.txt",
            i = range(N_EEMS_RUNS), 
            name = ['{name}']),
        script=eemsgridscript
    params:
        statname='gendist'
    output:
        "dists/{name}.gengriddist"
    script: "../" + eemsgridscript

geogriddistscript="scripts/geodistgrid.R"
rule geo_grid_dist:
    input:
        ipmap='eemsout/0/{name}/ipmap.txt',
        demes='eemsout/0/{name}/demes.txt',
        script=geogriddistscript
    output:
        "dists/{name}.geogriddist"
    script: "../" + geogriddistscript


rule dist_grid_all:
    input:
        "dists/{name}.gengriddist",
        "dists/{name}.eemsgriddist",
        "dists/{name}.geogriddist",
        "dists/{name}_dim2.pcgriddist",
        "dists/{name}_dim10.pcgriddist",
    output:
        "dists/{name}.grid"
    script: "../" + "scripts/merge_dists.R"

rule dist_all:
    input:
        "dists/{name}.gendist",
        "dists/{name}.eemsdist",
#        "dists/{name}.eems0dist",
        "dists/{name}_dim100.pcdist",
        "dists/{name}_dim10.pcdist",
        "dists/{name}_dim2.pcdist",
        "dists/{name}.geodist",
    output:
        "dists/{name}.dist"
    script: "../" + "scripts/merge_dists.R"

rule dist_ind_all:
    input:
        "dists/{name}.geninddist",
        "dists/{name}.eemsinddist",
#        "dists/{name}.eems0dist",
        "dists/{name}_dim100.pcinddist",
        "dists/{name}_dim10.pcinddist",
        "dists/{name}_dim2.pcinddist",
        "dists/{name}.geoinddist",
    output:
        "dists/{name}.inddist"
    script: "../" + "scripts/merge_dists.R"

rule rsqs:
    input:
        dists="dists/{name}.dist",
        pop_display=_POP_DISPLAY_,
        script="scripts/dist_rsq.R"
    output:
        rsq="dists/{name}.rsq"
    script:
        "../" + "scripts/dist_rsq.R"

rule rsqs_decile:
    input:
        dists="dists/{name}.dist",
        pop_display=_POP_DISPLAY_,
        script="scripts/dist_decile_rsq.R"
    output:
        rsq="dists/{name}.drsq"
    script:
        "../" + "scripts/dist_decile_rsq.R"

rule dist_ind:
    input:
        dists="dists/{name}.inddist",
        script="scripts/inddists.R"
    output:
        png="figures/inddists/{name}.png",
        rds="figures/inddists/{name}.rds"
    script:
        "../" + "scripts/inddists.R"

rule dist_plot:
    input:
        dists="dists/{name}.dist",
        pop_display=_POP_DISPLAY_,
        script="scripts/dists.R"
    output:
        png="figures/dists/{name}.png",
        rds="figures/dists/{name}.rds"
    script:
        "../" + "scripts/dists.R"

rule dists_local_vs_global_eems:
    input:
        "dists/{name}.eemsdist",
        "dists/{name}.popgrid",
